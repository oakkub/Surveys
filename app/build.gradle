apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        applicationId "com.oakkub.survey"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode Versions.version_code
        versionName Versions.version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            splits {
                density {
                    enable false
                }
                abi {
                    enable false
                }
            }
            aaptOptions {
                cruncherEnabled = false
            }
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-fresco.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-fresco.pro'
        }
    }

    flavorDimensions "stage"
    productFlavors {
        staging {
            applicationIdSuffix "staging"
            versionNameSuffix "-staging"
        }
        production {

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        staging {
            res.srcDirs += 'src/staging/res'
        }
        test {
            java.srcDirs += 'src/test/kotlin'
        }
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        disable "AllowBackup", "GoogleAppIndexingWarning"
    }
}

dependencies {
    implementation(
            fileTree(dir: 'libs', include: ['*.jar']),
            Dependencies.kotlin,

            Dependencies.support_v4,
            Dependencies.support_design,
            Dependencies.recycler_view,
            Dependencies.constraint_layout,

            Dependencies.retrofit,
            Dependencies.retrofit_converter_gson,
            Dependencies.retrofit_adapter_rxjava,

            Dependencies.okhttp,
            Dependencies.okhttp_interceptor,

            Dependencies.rxjava,
            Dependencies.rxandroid,

            Dependencies.arch_extensions,
            Dependencies.arch_common,
            Dependencies.android_ktx,

            Dependencies.fresco,
            Dependencies.fresco_okhttp,

            Dependencies.dagger,
            Dependencies.dagger_support
    )

    kapt(
            Dependencies.dagger_compiler,
            Dependencies.dagger_processor
    )

    testImplementation(
            Dependencies.junit,
            Dependencies.mockito,
            Dependencies.mock_web_server,
            Dependencies.robolectric,
            Dependencies.arch_testing
    )

    androidTestImplementation(
            Dependencies.test_runner,
            Dependencies.test_rules,
            Dependencies.espresso
    )
}
